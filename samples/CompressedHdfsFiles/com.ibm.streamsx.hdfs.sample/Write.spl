namespace com.ibm.streamsx.hdfs.sample;
use com.ibm.streamsx.hdfs::HDFSFileSink ;
use com.ibm.streamsx.hdfs::* ;

/**
 * Write compressed data into HDFS using Compress and Format operators.
 * 
 * This example shows how to write a compressed file into HDFS.  
 * * We generate synthetic data Beacon operator,
 * * Use the Format operator to put the tuples in csv format
 * * Compress the data with the Compress operator
 * * Write the compressed data to HDFS with HDFSFileSink.
 * 
 * To run, you need to supply an hdfsUri
 */
composite Write
{
	param
	expression<rstring> $hdfsUri : getSubmissionTimeValue("hdfsUri");

	graph
		stream<rstring message, int32 tupleNumber, float64 afloat> DataStream =
			Beacon()
		{
			param
				iterations : 10000000 ;
			output
				DataStream : message = "This is tuple number " +(rstring) IterationCount(),
					tupleNumber =(int32) IterationCount(), afloat = sqrt((float64)
					IterationCount()) ;
		}

		() as refSink = FileSink(DataStream)
		{
			param
				file : "reference.csv" ;
		}

		stream<blob ablob> BlobStream = Format(DataStream)
		{
			param
				format : csv ;
			output
				BlobStream : ablob = Output() ;
		}

		stream<blob ablob> CompressedStream = Compress(BlobStream)
		{
			param
				compression : gzip ;
		}

		() as hdfsSink = HDFSFileSink(CompressedStream)
		{
			param
				file : "testCompress.gz" ;
				hdfsUri : $hdfsUri;
		}
}
